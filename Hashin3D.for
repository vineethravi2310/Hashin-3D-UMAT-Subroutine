! WRITTEN BY VINEETH RAVI
! FOLLOWING IS A UMAT CODE FOR ORTHOTROPIC LINEAR ELASTIC MATERIAL
! THIS UMAT WORKS ONLY FOR C3D8 ELEMENTS
! VARIABLES FROM THE LETTER I TO N ARE CONSIDEREDED TO BE INTEGERS
! AND VARIABLES STARTING WITH OTHER LETTERS ARE DOUBLE PRECISION
! SO PLEASE BE CAREFUL IN NAMING THE VARIABLES
! EXAMPLES OF FEW DEFAULT INTEGERS : NTENS, NDI, NSHR

      SUBROUTINE UMAT(STRESS,STATEV,DDSDDE,SSE,SPD,SCD,
     1 RPL,DDSDDT,DRPLDE,DRPLDT,
     2 STRAN,DSTRAN,TIME,DTIME,TEMP,DTEMP,PREDEF,DPRED,CMNAME,
     3 NDI,NSHR,NTENS,NSTATV,PROPS,NPROPS,COORDS,DROT,PNEWDT,
     4 CELENT,DFGRD0,DFGRD1,NOEL,NPT,LAYER,KSPT,JSTEP,KINC)
      INCLUDE 'ABA_PARAM.INC'
C
      CHARACTER*80 CMNAME
      DIMENSION STRESS(NTENS),STATEV(NSTATV),
     1 DDSDDE(NTENS,NTENS),DDSDDT(NTENS),DRPLDE(NTENS),
     2 STRAN(NTENS),DSTRAN(NTENS),TIME(2),PREDEF(1),DPRED(1),
     3 PROPS(NPROPS),COORDS(3),DROT(3,3),DFGRD0(3,3),DFGRD1(3,3),
     4 JSTEP(4)
C      
      PARAMETER(ZERO = 0.0D0,ONE=1.0D0, TWO=2.0D0)
C	 
      E11 = PROPS(1)    ! YOUNGS MODULUS IN 1 DIRECTION
      E22 = PROPS(2)    ! YOUNGS MODULUS IN 2 DIRECTION
      E33 = PROPS(3)    ! YOUNGS MODULUS IN 3 DIRECTION
      PR12 = PROPS(4)   ! MAJOR POISSONS RATIO IN 1-2 PLANE
      PR13 = PROPS(5)   ! MAJOR POISSONS RATIO IN 1-3 PLANE
      PR23 = PROPS(6)   ! MAJOR POISSONS RATIO IN 2-3 PLANE
      G12 = PROPS(7)    ! SHEAR MODULUS IN 1-2 PLANE
      G13 = PROPS(8)    ! SHEAR MODULUS IN 1-3 PLANE
      G23 = PROPS(9)    ! SHEAR MODULUS IN 2-3 PLANE
	  
      XT = PROPS(10)
      XC = PROPS(11)
      YT = PROPS(12)
      YC = PROPS(13)
      S = PROPS(14)
      Q = PROPS(15)   

C	  
      PR21 = (E22*PR12)/E11  ! CALCULAION OF MINOR POISSONS RATIO
      PR31 = (E33*PR13)/E11
      PR32 = (E33*PR23)/E22
      DELTA = (ONE)/(ONE-PR12*PR21-PR23*PR32-PR13*PR31-TWO*PR21*PR32*PR13)
C	  
      DO I=1,NTENS  ! INITIALIZATION OF JACOBIAN MATRIX
          DO J=1,NTENS
              DDSDDE(I,J) = ZERO
          ENDDO
      ENDDO
C        
      DDSDDE(1,1) = (E11*DELTA*(ONE-PR23*PR32))
      DDSDDE(1,2) = (E11*DELTA*(PR21+PR31*PR23))
      DDSDDE(1,3) = (E11*DELTA*(PR31+PR21*PR32))
      DDSDDE(2,1) = (E11*DELTA*(PR21+PR31*PR23))
      DDSDDE(2,2) = (E22*DELTA*(ONE-PR13*PR31))
      DDSDDE(2,3) = (E22*DELTA*(PR32+PR12*PR31))
      DDSDDE(3,1) = (E11*DELTA*(PR31+PR21*PR32))
      DDSDDE(3,2) = (E22*DELTA*(PR32+PR12*PR31))
      DDSDDE(3,3) = (E33*DELTA*(ONE-PR12*PR21))
      DDSDDE(4,4) = G12
      DDSDDE(5,5) = G13
      DDSDDE(6,6) = G23
C        
      DO I=1, NTENS
          DO J=1, NTENS
              STRESS(J) = STRESS(J) + DDSDDE(J, I)*DSTRAN(I) ! UPDATING STRESS TENSOR
          END DO
      END DO
	  
      STATEV = 0
	  
      S11=STRESS(1) 
      S22=STRESS(2) 
      S33=STRESS(3) 
      S12=STRESS(4) 
      S13=STRESS(5) 
      S23=STRESS(6)

      IF(S11.GE. 0.0) THEN
          
          STATEV(1) = (S11/XT)**2 + (ONE/S**2)*(S12**2+S13**2)
      ELSE
          STATEV(2) = (-S11/XC)**2
      END IF
      
      IF((S22 + S33) .GE. 0.0) THEN
          
          STATEV(3) = ((S22 + S33)**2)/(YT**2) + ((S23**2 - S22*S33))/(Q**2) + ((S12**2)+(S13**2))/(S**2)
      ELSE
      
          STATEV(4) = (ONE/(TWO*Q)**2)*(S22 + S33)**2 + (ONE/Q**2)*(S23**2 - S22*S33) + (ONE/S**2)*((S12**2)+(S13**2)) + (S22 + S33)*(ONE/YC)*(((YC/(TWO*Q)))**2 - ONE)
	  
      END IF 
	  
C        
      RETURN
      END
